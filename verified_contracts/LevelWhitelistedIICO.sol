/** @title Interactive Coin Offering
 *  @author Clément Lesaege - <<span class="__cf_email__" data-cfemail="03606f666e666d77436f6670626664662d606c6e">[email protected]</span>&gt;&#13;
 */&#13;
&#13;
pragma solidity ^0.4.23;&#13;
&#13;
/**&#13;
 * @title ERC20Basic&#13;
 * @dev Simpler version of ERC20 interface&#13;
 * @dev see https://github.com/ethereum/EIPs/issues/179&#13;
 */&#13;
contract ERC20Basic {&#13;
  function totalSupply() public view returns (uint256);&#13;
  function balanceOf(address who) public view returns (uint256);&#13;
  function transfer(address to, uint256 value) public returns (bool);&#13;
  event Transfer(address indexed from, address indexed to, uint256 value);&#13;
}&#13;
&#13;
/**&#13;
 * @title ERC20 interface&#13;
 * @dev see https://github.com/ethereum/EIPs/issues/20&#13;
 */&#13;
contract ERC20 is ERC20Basic {&#13;
  function allowance(address owner, address spender) public view returns (uint256);&#13;
  function transferFrom(address from, address to, uint256 value) public returns (bool);&#13;
  function approve(address spender, uint256 value) public returns (bool);&#13;
  event Approval(address indexed owner, address indexed spender, uint256 value);&#13;
}&#13;
&#13;
/** @title Interactive Coin Offering&#13;
 *  This contract implements the Interactive Coin Offering token sale as described in this paper:&#13;
 *  https://people.cs.uchicago.edu/~teutsch/papers/ico.pdf&#13;
 *  Implementation details and modifications compared to the paper:&#13;
 *  -A fixed amount of tokens is sold. This allows more flexibility for the distribution of the remaining tokens (rounds, team tokens which can be preallocated, non-initial sell of some cryptographic assets).&#13;
 *  -The valuation pointer is only moved when the sale is over. This greatly reduces the amount of write operations and code complexity. However, at least one party must make one or multiple calls to finalize the sale.&#13;
 *  -Buckets are not used as they are not required and increase code complexity.&#13;
 *  -The bid submitter must provide the insertion spot. A search of the insertion spot is still done in the contract just in case the one provided was wrong or other bids were added between when the TX got signed and executed, but giving the search starting point greatly lowers gas consumption.&#13;
 *  -Automatic withdrawals are only possible at the end of the sale. This decreases code complexity and possible interactions between different parts of the code.&#13;
 *  -We put a full bonus, free withdrawal period at the beginning. This allows everyone to have a chance to place bids with full bonus and avoids clogging the network just after the sale starts. Note that at this moment, no information can be taken for granted as parties can withdraw freely.&#13;
 *  -Calling the fallback function while sending ETH places a bid with an infinite maximum valuation. This allows buyers who want to buy no matter the price not need to use a specific interface and just send ETH. Without ETH, a call to the fallback function redeems the bids of the caller.&#13;
 */&#13;
contract IICO {&#13;
&#13;
    /* *** General *** */&#13;
    address public owner;       // The one setting up the contract.&#13;
    address public beneficiary; // The address which will get the funds.&#13;
&#13;
    /* *** Bid *** */&#13;
    uint constant HEAD = 0;            // Minimum value used for both the maxValuation and bidID of the head of the linked list.&#13;
    uint constant TAIL = uint(-1);     // Maximum value used for both the maxValuation and bidID of the tail of the linked list.&#13;
    uint constant INFINITY = uint(-2); // A value so high that a bid using it is guaranteed to succeed. Still lower than TAIL to be placed before TAIL.&#13;
    // A bid to buy tokens as long as the personal maximum valuation is not exceeded.&#13;
    // Bids are in a sorted doubly linked list.&#13;
    // They are sorted in ascending order by (maxValuation,bidID) where bidID is the ID and key of the bid in the mapping.&#13;
    // The list contains two artificial bids HEAD and TAIL having respectively the minimum and maximum bidID and maxValuation.&#13;
    struct Bid {&#13;
        /* *** Linked List Members *** */&#13;
        uint prev;            // bidID of the previous element.&#13;
        uint next;            // bidID of the next element.&#13;
        /* ***     Bid Members     *** */&#13;
        uint maxValuation;    // Maximum valuation in wei beyond which the contributor prefers refund.&#13;
        uint contrib;         // Contribution in wei.&#13;
        uint bonus;           // The numerator of the bonus that will be divided by BONUS_DIVISOR.&#13;
        address contributor;  // The contributor who placed the bid.&#13;
        bool withdrawn;       // True if the bid has been withdrawn.&#13;
        bool redeemed;        // True if the ETH or tokens have been redeemed.&#13;
    }&#13;
    mapping (uint =&gt; Bid) public bids; // Map bidID to bid.&#13;
    mapping (address =&gt; uint[]) public contributorBidIDs; // Map contributor to a list of its bid ID.&#13;
    uint public lastBidID = 0; // The last bidID not accounting TAIL.&#13;
&#13;
    /* *** Sale parameters *** */&#13;
    uint public startTime;                      // When the sale starts.&#13;
    uint public endFullBonusTime;               // When the full bonus period ends.&#13;
    uint public withdrawalLockTime;             // When the contributors can't withdraw their bids manually anymore.&#13;
    uint public endTime;                        // When the sale ends.&#13;
    ERC20 public token;                         // The token which is sold.&#13;
    uint public tokensForSale;                  // The amount of tokens which will be sold.&#13;
    uint public maxBonus;                       // The maximum bonus. Will be normalized by BONUS_DIVISOR. For example for a 20% bonus, _maxBonus must be 0.2 * BONUS_DIVISOR.&#13;
    uint constant BONUS_DIVISOR = 1E9;          // The quantity we need to divide by to normalize the bonus.&#13;
&#13;
    /* *** Finalization variables *** */&#13;
    bool public finalized;                 // True when the cutting bid has been found. The following variables are final only after finalized==true.&#13;
    uint public cutOffBidID = TAIL;        // The first accepted bid. All bids after it are accepted.&#13;
    uint public sumAcceptedContrib;        // The sum of accepted contributions.&#13;
    uint public sumAcceptedVirtualContrib; // The sum of virtual (taking into account bonuses) contributions.&#13;
&#13;
    /* *** Events *** */&#13;
    event BidSubmitted(address indexed contributor, uint indexed bidID, uint indexed time);&#13;
&#13;
    /* *** Modifiers *** */&#13;
    modifier onlyOwner{ require(owner == msg.sender); _; }&#13;
&#13;
    /* *** Functions Modifying the state *** */&#13;
&#13;
    /** @dev Constructor. First contract set up (tokens will also need to be transferred to the contract and then setToken needs to be called to finish the setup).&#13;
     *  @param _startTime Time the sale will start in seconds since the Unix Epoch.&#13;
     *  @param _fullBonusLength Amount of seconds the sale lasts in the full bonus period.&#13;
     *  @param _partialWithdrawalLength Amount of seconds the sale lasts in the partial withdrawal period.&#13;
     *  @param _withdrawalLockUpLength Amount of seconds the sale lasts in the withdrawal lockup period.&#13;
     *  @param _maxBonus The maximum bonus. Will be normalized by BONUS_DIVISOR. For example for a 20% bonus, _maxBonus must be 0.2 * BONUS_DIVISOR.&#13;
     *  @param _beneficiary The party which will get the funds of the token sale.&#13;
     */&#13;
    function IICO(uint _startTime, uint _fullBonusLength, uint _partialWithdrawalLength, uint _withdrawalLockUpLength, uint _maxBonus, address _beneficiary) public {&#13;
        owner = msg.sender;&#13;
        startTime = _startTime;&#13;
        endFullBonusTime = startTime + _fullBonusLength;&#13;
        withdrawalLockTime = endFullBonusTime + _partialWithdrawalLength;&#13;
        endTime = withdrawalLockTime + _withdrawalLockUpLength;&#13;
        maxBonus = _maxBonus;&#13;
        beneficiary = _beneficiary;&#13;
&#13;
        // Add the virtual bids. This simplifies other functions.&#13;
        bids[HEAD] = Bid({&#13;
            prev: TAIL,&#13;
            next: TAIL,&#13;
            maxValuation: HEAD,&#13;
            contrib: 0,&#13;
            bonus: 0,&#13;
            contributor: address(0),&#13;
            withdrawn: false,&#13;
            redeemed: false&#13;
        });&#13;
        bids[TAIL] = Bid({&#13;
            prev: HEAD,&#13;
            next: HEAD,&#13;
            maxValuation: TAIL,&#13;
            contrib: 0,&#13;
            bonus: 0,&#13;
            contributor: address(0),&#13;
            withdrawn: false,&#13;
            redeemed: false&#13;
        });&#13;
    }&#13;
&#13;
    /** @dev Set the token. Must only be called after the IICO contract receives the tokens to be sold.&#13;
     *  @param _token The token to be sold.&#13;
     */&#13;
    function setToken(ERC20 _token) public onlyOwner {&#13;
        require(address(token) == address(0)); // Make sure the token is not already set.&#13;
&#13;
        token = _token;&#13;
        tokensForSale = token.balanceOf(this);&#13;
    }&#13;
&#13;
    /** @dev Submit a bid. The caller must give the exact position the bid must be inserted into in the list.&#13;
     *  In practice, use searchAndBid to avoid the position being incorrect due to a new bid being inserted and changing the position the bid must be inserted at.&#13;
     *  @param _maxValuation The maximum valuation given by the contributor. If the amount raised is higher, the bid is cancelled and the contributor refunded because it prefers a refund instead of this level of dilution. To buy no matter what, use INFINITY.&#13;
     *  @param _next The bidID of the next bid in the list.&#13;
     */&#13;
    function submitBid(uint _maxValuation, uint _next) public payable {&#13;
        Bid storage nextBid = bids[_next];&#13;
        uint prev = nextBid.prev;&#13;
        Bid storage prevBid = bids[prev];&#13;
        require(_maxValuation &gt;= prevBid.maxValuation &amp;&amp; _maxValuation &lt; nextBid.maxValuation); // The new bid maxValuation is higher than the previous one and strictly lower than the next one.&#13;
        require(now &gt;= startTime &amp;&amp; now &lt; endTime); // Check that the bids are still open.&#13;
&#13;
        ++lastBidID; // Increment the lastBidID. It will be the new bid's ID.&#13;
        // Update the pointers of neighboring bids.&#13;
        prevBid.next = lastBidID;&#13;
        nextBid.prev = lastBidID;&#13;
&#13;
        // Insert the bid.&#13;
        bids[lastBidID] = Bid({&#13;
            prev: prev,&#13;
            next: _next,&#13;
            maxValuation: _maxValuation,&#13;
            contrib: msg.value,&#13;
            bonus: bonus(),&#13;
            contributor: msg.sender,&#13;
            withdrawn: false,&#13;
            redeemed: false&#13;
        });&#13;
&#13;
        // Add the bid to the list of bids by this contributor.&#13;
        contributorBidIDs[msg.sender].push(lastBidID);&#13;
&#13;
        // Emit event&#13;
        emit BidSubmitted(msg.sender, lastBidID, now);&#13;
    }&#13;
&#13;
&#13;
    /** @dev Search for the correct insertion spot and submit a bid.&#13;
     *  This function is O(n), where n is the amount of bids between the initial search position and the insertion position.&#13;
     *  The UI must first call search to find the best point to start the search such that it consumes the least amount of gas possible.&#13;
     *  Using this function instead of calling submitBid directly prevents it from failing in the case where new bids are added before the transaction is executed.&#13;
     *  @param _maxValuation The maximum valuation given by the contributor. If the amount raised is higher, the bid is cancelled and the contributor refunded because it prefers a refund instead of this level of dilution. To buy no matter what, use INFINITY.&#13;
     *  @param _next The bidID of the next bid in the list.&#13;
     */&#13;
    function searchAndBid(uint _maxValuation, uint _next) public payable {&#13;
        submitBid(_maxValuation, search(_maxValuation,_next));&#13;
    }&#13;
&#13;
    /** @dev Withdraw a bid. Can only be called before the end of the withdrawal lock period.&#13;
     *  Withdrawing a bid reduces its bonus by 1/3.&#13;
     *  For retrieving ETH after an automatic withdrawal, use the redeem function.&#13;
     *  @param _bidID The ID of the bid to withdraw.&#13;
     */&#13;
    function withdraw(uint _bidID) public {&#13;
        Bid storage bid = bids[_bidID];&#13;
        require(msg.sender == bid.contributor);&#13;
        require(now &lt; withdrawalLockTime);&#13;
        require(!bid.withdrawn);&#13;
&#13;
        bid.withdrawn = true;&#13;
&#13;
        // Before endFullBonusTime, everything is refunded. Otherwise, an amount decreasing linearly from endFullBonusTime to withdrawalLockTime is refunded.&#13;
        uint refund = (now &lt; endFullBonusTime) ? bid.contrib : (bid.contrib * (withdrawalLockTime - now)) / (withdrawalLockTime - endFullBonusTime);&#13;
        assert(refund &lt;= bid.contrib); // Make sure that we don't refund more than the contribution. Would a bug arise, we prefer blocking withdrawal than letting someone steal money.&#13;
        bid.contrib -= refund;&#13;
        bid.bonus = (bid.bonus * 2) / 3; // Reduce the bonus by 1/3.&#13;
&#13;
        msg.sender.transfer(refund);&#13;
    }&#13;
&#13;
    /** @dev Finalize by finding the cut-off bid.&#13;
     *  Since the amount of bids is not bounded, this function may have to be called multiple times.&#13;
     *  The function is O(min(n,_maxIt)) where n is the amount of bids. In total it will perform O(n) computations, possibly in multiple calls.&#13;
     *  Each call only has a O(1) storage write operations.&#13;
     *  @param _maxIt The maximum amount of bids to go through. This value must be set in order to not exceed the gas limit.&#13;
     */&#13;
    function finalize(uint _maxIt) public {&#13;
        require(now &gt;= endTime);&#13;
        require(!finalized);&#13;
&#13;
        // Make local copies of the finalization variables in order to avoid modifying storage in order to save gas.&#13;
        uint localCutOffBidID = cutOffBidID;&#13;
        uint localSumAcceptedContrib = sumAcceptedContrib;&#13;
        uint localSumAcceptedVirtualContrib = sumAcceptedVirtualContrib;&#13;
&#13;
        // Search for the cut-off bid while adding the contributions.&#13;
        for (uint it = 0; it &lt; _maxIt &amp;&amp; !finalized; ++it) {&#13;
            Bid storage bid = bids[localCutOffBidID];&#13;
            if (bid.contrib+localSumAcceptedContrib &lt; bid.maxValuation) { // We haven't found the cut-off yet.&#13;
                localSumAcceptedContrib        += bid.contrib;&#13;
                localSumAcceptedVirtualContrib += bid.contrib + (bid.contrib * bid.bonus) / BONUS_DIVISOR;&#13;
                localCutOffBidID = bid.prev; // Go to the previous bid.&#13;
            } else { // We found the cut-off. This bid will be taken partially.&#13;
                finalized = true;&#13;
                uint contribCutOff = bid.maxValuation &gt;= localSumAcceptedContrib ? bid.maxValuation - localSumAcceptedContrib : 0; // The amount of the contribution of the cut-off bid that can stay in the sale without spilling over the maxValuation.&#13;
                contribCutOff = contribCutOff &lt; bid.contrib ? contribCutOff : bid.contrib; // The amount that stays in the sale should not be more than the original contribution. This line is not required but it is added as an extra security measure.&#13;
                bid.contributor.send(bid.contrib-contribCutOff); // Send the non-accepted part. Use send in order to not block if the contributor's fallback reverts.&#13;
                bid.contrib = contribCutOff; // Update the contribution value.&#13;
                localSumAcceptedContrib += bid.contrib;&#13;
                localSumAcceptedVirtualContrib += bid.contrib + (bid.contrib * bid.bonus) / BONUS_DIVISOR;&#13;
                beneficiary.send(localSumAcceptedContrib); // Use send in order to not block if the beneficiary's fallback reverts.&#13;
            }&#13;
        }&#13;
&#13;
        // Update storage.&#13;
        cutOffBidID = localCutOffBidID;&#13;
        sumAcceptedContrib = localSumAcceptedContrib;&#13;
        sumAcceptedVirtualContrib = localSumAcceptedVirtualContrib;&#13;
    }&#13;
&#13;
    /** @dev Redeem a bid. If the bid is accepted, send the tokens, otherwise refund the ETH.&#13;
     *  Note that anyone can call this function, not only the party which made the bid.&#13;
     *  @param _bidID ID of the bid to withdraw.&#13;
     */&#13;
    function redeem(uint _bidID) public {&#13;
        Bid storage bid = bids[_bidID];&#13;
        Bid storage cutOffBid = bids[cutOffBidID];&#13;
        require(finalized);&#13;
        require(!bid.redeemed);&#13;
&#13;
        bid.redeemed=true;&#13;
        if (bid.maxValuation &gt; cutOffBid.maxValuation || (bid.maxValuation == cutOffBid.maxValuation &amp;&amp; _bidID &gt;= cutOffBidID)) // Give tokens if the bid is accepted.&#13;
            require(token.transfer(bid.contributor, (tokensForSale * (bid.contrib + (bid.contrib * bid.bonus) / BONUS_DIVISOR)) / sumAcceptedVirtualContrib));&#13;
        else                                                                                            // Reimburse ETH otherwise.&#13;
            bid.contributor.transfer(bid.contrib);&#13;
    }&#13;
&#13;
    /** @dev Fallback. Make a bid if ETH are sent. Redeem all the bids of the contributor otherwise.&#13;
     *  Note that the contributor could make this function go out of gas if it has too much bids. This in not a problem as it is still possible to redeem using the redeem function directly.&#13;
     *  This allows users to bid and get their tokens back using only send operations.&#13;
     */&#13;
    function () public payable {&#13;
        if (msg.value != 0 &amp;&amp; now &gt;= startTime &amp;&amp; now &lt; endTime) // Make a bid with an infinite maxValuation if some ETH was sent.&#13;
            submitBid(INFINITY, TAIL);&#13;
        else if (msg.value == 0 &amp;&amp; finalized)                    // Else, redeem all the non redeemed bids if no ETH was sent.&#13;
            for (uint i = 0; i &lt; contributorBidIDs[msg.sender].length; ++i)&#13;
            {&#13;
                if (!bids[contributorBidIDs[msg.sender][i]].redeemed)&#13;
                    redeem(contributorBidIDs[msg.sender][i]);&#13;
            }&#13;
        else                                                     // Otherwise, no actions are possible.&#13;
            revert();&#13;
    }&#13;
&#13;
    /* *** View Functions *** */&#13;
&#13;
    /** @dev Search for the correct insertion spot of a bid.&#13;
     *  This function is O(n), where n is the amount of bids between the initial search position and the insertion position.&#13;
     *  @param _maxValuation The maximum valuation given by the contributor. Or INFINITY if no maximum valuation is given.&#13;
     *  @param _nextStart The bidID of the next bid from the initial position to start the search from.&#13;
     *  @return nextInsert The bidID of the next bid from the position the bid must be inserted at.&#13;
     */&#13;
    function search(uint _maxValuation, uint _nextStart) view public returns(uint nextInsert) {&#13;
        uint next = _nextStart;&#13;
        bool found;&#13;
&#13;
        while(!found) { // While we aren't at the insertion point.&#13;
            Bid storage nextBid = bids[next];&#13;
            uint prev = nextBid.prev;&#13;
            Bid storage prevBid = bids[prev];&#13;
&#13;
            if (_maxValuation &lt; prevBid.maxValuation)       // It should be inserted before.&#13;
                next = prev;&#13;
            else if (_maxValuation &gt;= nextBid.maxValuation) // It should be inserted after. The second value we sort by is bidID. Those are increasing, thus if the next bid is of the same maxValuation, we should insert after it.&#13;
                next = nextBid.next;&#13;
            else                                // We found the insertion point.&#13;
                found = true;&#13;
        }&#13;
&#13;
        return next;&#13;
    }&#13;
&#13;
    /** @dev Return the current bonus. The bonus only changes in 1/BONUS_DIVISOR increments.&#13;
     *  @return b The bonus expressed in 1/BONUS_DIVISOR. Will be normalized by BONUS_DIVISOR. For example for a 20% bonus, _maxBonus must be 0.2 * BONUS_DIVISOR.&#13;
     */&#13;
    function bonus() public view returns(uint b) {&#13;
        if (now &lt; endFullBonusTime) // Full bonus.&#13;
            return maxBonus;&#13;
        else if (now &gt; endTime)     // Assume no bonus after end.&#13;
            return 0;&#13;
        else                        // Compute the bonus decreasing linearly from endFullBonusTime to endTime.&#13;
            return (maxBonus * (endTime - now)) / (endTime - endFullBonusTime);&#13;
    }&#13;
&#13;
    /** @dev Get the total contribution of an address.&#13;
     *  This can be used for a KYC threshold.&#13;
     *  This function is O(n) where n is the amount of bids made by the contributor.&#13;
     *  This means that the contributor can make totalContrib(contributor) revert due to an out of gas error on purpose.&#13;
     *  @param _contributor The contributor whose contribution will be returned.&#13;
     *  @return contribution The total contribution of the contributor.&#13;
     */&#13;
    function totalContrib(address _contributor) public view returns (uint contribution) {&#13;
        for (uint i = 0; i &lt; contributorBidIDs[_contributor].length; ++i)&#13;
            contribution += bids[contributorBidIDs[_contributor][i]].contrib;&#13;
    }&#13;
&#13;
    /* *** Interface Views *** */&#13;
&#13;
    /** @dev Get the current valuation and cut off bid's details.&#13;
     *  This function is O(n), where n is the amount of bids. This could exceed the gas limit, therefore this function should only be used for interface display and not by other contracts.&#13;
     *  @return The current valuation and cut off bid's details.&#13;
     */&#13;
    function valuationAndCutOff() public view returns (uint valuation, uint virtualValuation, uint currentCutOffBidID, uint currentCutOffBidmaxValuation, uint currentCutOffBidContrib) {&#13;
        currentCutOffBidID = bids[TAIL].prev;&#13;
&#13;
        // Loop over all bids or until cut off bid is found&#13;
        while (currentCutOffBidID != HEAD) {&#13;
            Bid storage bid = bids[currentCutOffBidID];&#13;
            if (bid.contrib + valuation &lt; bid.maxValuation) { // We haven't found the cut-off yet.&#13;
                valuation += bid.contrib;&#13;
                virtualValuation += bid.contrib + (bid.contrib * bid.bonus) / BONUS_DIVISOR;&#13;
                currentCutOffBidID = bid.prev; // Go to the previous bid.&#13;
            } else { // We found the cut-off bid. This bid will be taken partially.&#13;
                currentCutOffBidContrib = bid.maxValuation &gt;= valuation ? bid.maxValuation - valuation : 0; // The amount of the contribution of the cut-off bid that can stay in the sale without spilling over the maxValuation.&#13;
                valuation += currentCutOffBidContrib;&#13;
                virtualValuation += currentCutOffBidContrib + (currentCutOffBidContrib * bid.bonus) / BONUS_DIVISOR;&#13;
                break;&#13;
            }&#13;
        }&#13;
&#13;
        currentCutOffBidmaxValuation = bids[currentCutOffBidID].maxValuation;&#13;
    }&#13;
}&#13;
&#13;
/** @title Level Whitelisted Interactive Coin Offering&#13;
 *  This contract implements an Interactive Coin Offering with two whitelists:&#13;
 *  - The base one, with limited contribution.&#13;
 *  - The reinforced one, with unlimited contribution.&#13;
 */&#13;
contract LevelWhitelistedIICO is IICO {&#13;
    &#13;
    uint public maximumBaseContribution;&#13;
    mapping (address =&gt; bool) public baseWhitelist; // True if in the base whitelist (has a contribution limit).&#13;
    mapping (address =&gt; bool) public reinforcedWhitelist; // True if in the reinforced whitelist (does not have a contribution limit).&#13;
    address public whitelister; // The party which can add or remove people from the whitelist.&#13;
    &#13;
    modifier onlyWhitelister{ require(whitelister == msg.sender); _; }&#13;
    &#13;
    /** @dev Constructor. First contract set up (tokens will also need to be transferred to the contract and then setToken needs to be called to finish the setup).&#13;
     *  @param _startTime Time the sale will start in seconds since the Unix Epoch.&#13;
     *  @param _fullBonusLength Amount of seconds the sale lasts in the full bonus period.&#13;
     *  @param _partialWithdrawalLength Amount of seconds the sale lasts in the partial withdrawal period.&#13;
     *  @param _withdrawalLockUpLength Amount of seconds the sale lasts in the withdrawal lockup period.&#13;
     *  @param _maxBonus The maximum bonus. Will be normalized by BONUS_DIVISOR. For example for a 20% bonus, _maxBonus must be 0.2 * BONUS_DIVISOR.&#13;
     *  @param _beneficiary The party which will get the funds of the token sale.&#13;
     *  @param _maximumBaseContribution The maximum contribution for buyers on the base list.&#13;
     */&#13;
    function LevelWhitelistedIICO(uint _startTime, uint _fullBonusLength, uint _partialWithdrawalLength, uint _withdrawalLockUpLength, uint _maxBonus, address _beneficiary, uint _maximumBaseContribution) IICO(_startTime,_fullBonusLength,_partialWithdrawalLength,_withdrawalLockUpLength,_maxBonus,_beneficiary) public {&#13;
        maximumBaseContribution=_maximumBaseContribution;&#13;
    }&#13;
    &#13;
    /** @dev Submit a bid. The caller must give the exact position the bid must be inserted into in the list.&#13;
     *  In practice, use searchAndBid to avoid the position being incorrect due to a new bid being inserted and changing the position the bid must be inserted at.&#13;
     *  @param _maxValuation The maximum valuation given by the contributor. If the amount raised is higher, the bid is cancelled and the contributor refunded because it prefers a refund instead of this level of dilution. To buy no matter what, use INFINITY.&#13;
     *  @param _next The bidID of the next bid in the list.&#13;
     */&#13;
    function submitBid(uint _maxValuation, uint _next) public payable {&#13;
        require(reinforcedWhitelist[msg.sender] || (baseWhitelist[msg.sender] &amp;&amp; (msg.value + totalContrib(msg.sender) &lt;= maximumBaseContribution))); // Check if the buyer is in the reinforced whitelist or if it is on the base one and this would not make its total contribution exceed the limit.&#13;
        super.submitBid(_maxValuation,_next);&#13;
    }&#13;
    &#13;
    /** @dev Set the whitelister.&#13;
     *  @param _whitelister The whitelister.&#13;
     */&#13;
    function setWhitelister(address _whitelister) public onlyOwner {&#13;
        whitelister=_whitelister;&#13;
    }&#13;
    &#13;
    /** @dev Add buyers to the base whitelist.&#13;
     *  @param _buyersToWhitelist Buyers to add to the whitelist.&#13;
     */&#13;
    function addBaseWhitelist(address[] _buyersToWhitelist) public onlyWhitelister {&#13;
        for(uint i=0;i&lt;_buyersToWhitelist.length;++i)&#13;
            baseWhitelist[_buyersToWhitelist[i]]=true;&#13;
    }&#13;
    &#13;
    /** @dev Add buyers to the reinforced whitelist.&#13;
     *  @param _buyersToWhitelist Buyers to add to the whitelist.&#13;
     */&#13;
    function addReinforcedWhitelist(address[] _buyersToWhitelist) public onlyWhitelister {&#13;
        for(uint i=0;i&lt;_buyersToWhitelist.length;++i)&#13;
            reinforcedWhitelist[_buyersToWhitelist[i]]=true;&#13;
    }&#13;
    &#13;
    /** @dev Remove buyers from the base whitelist.&#13;
     *  @param _buyersToRemove Buyers to remove from the whitelist.&#13;
     */&#13;
    function removeBaseWhitelist(address[] _buyersToRemove) public onlyWhitelister {&#13;
        for(uint i=0;i&lt;_buyersToRemove.length;++i)&#13;
            baseWhitelist[_buyersToRemove[i]]=false;&#13;
    }&#13;
    &#13;
    /** @dev Remove buyers from the reinforced whitelist.&#13;
     *  @param _buyersToRemove Buyers to remove from the whitelist.&#13;
     */&#13;
    function removeReinforcedWhitelist(address[] _buyersToRemove) public onlyWhitelister {&#13;
        for(uint i=0;i&lt;_buyersToRemove.length;++i)&#13;
            reinforcedWhitelist[_buyersToRemove[i]]=false;&#13;
    }&#13;
&#13;
}