/**
 *  @title Arbitrable Permission List
 *  @author Clément Lesaege - <<a href="/cdn-cgi/l/email-protection" class="__cf_email__" data-cfemail="90f3fcf5fdf5fee4d0fcf5e3f1f5f7f5bef3fffd">[email protected]</a>&gt;&#13;
 *  This code has undertaken a 15 ETH max price bug bounty program.&#13;
 */&#13;
&#13;
pragma solidity ^0.4.23;&#13;
&#13;
/**&#13;
 *  @title Permission Interface&#13;
 *  This is a permission interface for arbitrary values. The values can be cast to the required types.&#13;
 */&#13;
interface PermissionInterface{&#13;
    /* External */&#13;
&#13;
    /**&#13;
     *  @dev Return true if the value is allowed.&#13;
     *  @param _value The value we want to check.&#13;
     *  @return allowed True if the value is allowed, false otherwise.&#13;
     */&#13;
    function isPermitted(bytes32 _value) external view returns (bool allowed);&#13;
}&#13;
&#13;
/** @title Arbitrator&#13;
 *  Arbitrator abstract contract.&#13;
 *  When developing arbitrator contracts we need to:&#13;
 *  -Define the functions for dispute creation (createDispute) and appeal (appeal). Don't forget to store the arbitrated contract and the disputeID (which should be unique, use nbDisputes).&#13;
 *  -Define the functions for cost display (arbitrationCost and appealCost).&#13;
 *  -Allow giving rulings. For this a function must call arbitrable.rule(disputeID,ruling).&#13;
 */&#13;
contract Arbitrator{&#13;
&#13;
    enum DisputeStatus {Waiting, Appealable, Solved}&#13;
&#13;
    modifier requireArbitrationFee(bytes _extraData) {require(msg.value&gt;=arbitrationCost(_extraData)); _;}&#13;
    modifier requireAppealFee(uint _disputeID, bytes _extraData) {require(msg.value&gt;=appealCost(_disputeID, _extraData)); _;}&#13;
&#13;
    /** @dev To be raised when a dispute can be appealed.&#13;
     *  @param _disputeID ID of the dispute.&#13;
     */&#13;
    event AppealPossible(uint _disputeID);&#13;
&#13;
    /** @dev To be raised when a dispute is created.&#13;
     *  @param _disputeID ID of the dispute.&#13;
     *  @param _arbitrable The contract which created the dispute.&#13;
     */&#13;
    event DisputeCreation(uint indexed _disputeID, Arbitrable _arbitrable);&#13;
&#13;
    /** @dev To be raised when the current ruling is appealed.&#13;
     *  @param _disputeID ID of the dispute.&#13;
     *  @param _arbitrable The contract which created the dispute.&#13;
     */&#13;
    event AppealDecision(uint indexed _disputeID, Arbitrable _arbitrable);&#13;
&#13;
    /** @dev Create a dispute. Must be called by the arbitrable contract.&#13;
     *  Must be paid at least arbitrationCost(_extraData).&#13;
     *  @param _choices Amount of choices the arbitrator can make in this dispute.&#13;
     *  @param _extraData Can be used to give additional info on the dispute to be created.&#13;
     *  @return disputeID ID of the dispute created.&#13;
     */&#13;
    function createDispute(uint _choices, bytes _extraData) public requireArbitrationFee(_extraData) payable returns(uint disputeID)  {}&#13;
&#13;
    /** @dev Compute the cost of arbitration. It is recommended not to increase it often, as it can be highly time and gas consuming for the arbitrated contracts to cope with fee augmentation.&#13;
     *  @param _extraData Can be used to give additional info on the dispute to be created.&#13;
     *  @return fee Amount to be paid.&#13;
     */&#13;
    function arbitrationCost(bytes _extraData) public constant returns(uint fee);&#13;
&#13;
    /** @dev Appeal a ruling. Note that it has to be called before the arbitrator contract calls rule.&#13;
     *  @param _disputeID ID of the dispute to be appealed.&#13;
     *  @param _extraData Can be used to give extra info on the appeal.&#13;
     */&#13;
    function appeal(uint _disputeID, bytes _extraData) public requireAppealFee(_disputeID,_extraData) payable {&#13;
        emit AppealDecision(_disputeID, Arbitrable(msg.sender));&#13;
    }&#13;
&#13;
    /** @dev Compute the cost of appeal. It is recommended not to increase it often, as it can be higly time and gas consuming for the arbitrated contracts to cope with fee augmentation.&#13;
     *  @param _disputeID ID of the dispute to be appealed.&#13;
     *  @param _extraData Can be used to give additional info on the dispute to be created.&#13;
     *  @return fee Amount to be paid.&#13;
     */&#13;
    function appealCost(uint _disputeID, bytes _extraData) public constant returns(uint fee);&#13;
&#13;
    /** @dev Return the status of a dispute.&#13;
     *  @param _disputeID ID of the dispute to rule.&#13;
     *  @return status The status of the dispute.&#13;
     */&#13;
    function disputeStatus(uint _disputeID) public constant returns(DisputeStatus status);&#13;
&#13;
    /** @dev Return the current ruling of a dispute. This is useful for parties to know if they should appeal.&#13;
     *  @param _disputeID ID of the dispute.&#13;
     *  @return ruling The ruling which has been given or the one which will be given if there is no appeal.&#13;
     */&#13;
    function currentRuling(uint _disputeID) public constant returns(uint ruling);&#13;
&#13;
}&#13;
&#13;
/** @title Arbitrable&#13;
 *  Arbitrable abstract contract.&#13;
 *  When developing arbitrable contracts, we need to:&#13;
 *  -Define the action taken when a ruling is received by the contract. We should do so in executeRuling.&#13;
 *  -Allow dispute creation. For this a function must:&#13;
 *      -Call arbitrator.createDispute.value(_fee)(_choices,_extraData);&#13;
 *      -Create the event Dispute(_arbitrator,_disputeID,_rulingOptions);&#13;
 */&#13;
contract Arbitrable{&#13;
    Arbitrator public arbitrator;&#13;
    bytes public arbitratorExtraData; // Extra data to require particular dispute and appeal behaviour.&#13;
&#13;
    modifier onlyArbitrator {require(msg.sender==address(arbitrator)); _;}&#13;
&#13;
    /** @dev To be raised when a ruling is given.&#13;
     *  @param _arbitrator The arbitrator giving the ruling.&#13;
     *  @param _disputeID ID of the dispute in the Arbitrator contract.&#13;
     *  @param _ruling The ruling which was given.&#13;
     */&#13;
    event Ruling(Arbitrator indexed _arbitrator, uint indexed _disputeID, uint _ruling);&#13;
&#13;
    /** @dev To be emmited when meta-evidence is submitted.&#13;
     *  @param _metaEvidenceID Unique identifier of meta-evidence.&#13;
     *  @param _evidence A link to the meta-evidence JSON.&#13;
     */&#13;
    event MetaEvidence(uint indexed _metaEvidenceID, string _evidence);&#13;
&#13;
    /** @dev To be emmited when a dispute is created to link the correct meta-evidence to the disputeID&#13;
     *  @param _arbitrator The arbitrator of the contract.&#13;
     *  @param _disputeID ID of the dispute in the Arbitrator contract.&#13;
     *  @param _metaEvidenceID Unique identifier of meta-evidence.&#13;
     */&#13;
    event Dispute(Arbitrator indexed _arbitrator, uint indexed _disputeID, uint _metaEvidenceID);&#13;
&#13;
    /** @dev To be raised when evidence are submitted. Should point to the ressource (evidences are not to be stored on chain due to gas considerations).&#13;
     *  @param _arbitrator The arbitrator of the contract.&#13;
     *  @param _disputeID ID of the dispute in the Arbitrator contract.&#13;
     *  @param _party The address of the party submiting the evidence. Note that 0x0 refers to evidence not submitted by any party.&#13;
     *  @param _evidence A URI to the evidence JSON file whose name should be its keccak256 hash followed by .json.&#13;
     */&#13;
    event Evidence(Arbitrator indexed _arbitrator, uint indexed _disputeID, address _party, string _evidence);&#13;
&#13;
    /** @dev Constructor. Choose the arbitrator.&#13;
     *  @param _arbitrator The arbitrator of the contract.&#13;
     *  @param _arbitratorExtraData Extra data for the arbitrator.&#13;
     */&#13;
    constructor(Arbitrator _arbitrator, bytes _arbitratorExtraData) public {&#13;
        arbitrator = _arbitrator;&#13;
        arbitratorExtraData = _arbitratorExtraData;&#13;
    }&#13;
&#13;
    /** @dev Give a ruling for a dispute. Must be called by the arbitrator.&#13;
     *  The purpose of this function is to ensure that the address calling it has the right to rule on the contract.&#13;
     *  @param _disputeID ID of the dispute in the Arbitrator contract.&#13;
     *  @param _ruling Ruling given by the arbitrator. Note that 0 is reserved for "Not able/wanting to make a decision".&#13;
     */&#13;
    function rule(uint _disputeID, uint _ruling) public onlyArbitrator {&#13;
        emit Ruling(Arbitrator(msg.sender),_disputeID,_ruling);&#13;
&#13;
        executeRuling(_disputeID,_ruling);&#13;
    }&#13;
&#13;
&#13;
    /** @dev Execute a ruling of a dispute.&#13;
     *  @param _disputeID ID of the dispute in the Arbitrator contract.&#13;
     *  @param _ruling Ruling given by the arbitrator. Note that 0 is reserved for "Not able/wanting to make a decision".&#13;
     */&#13;
    function executeRuling(uint _disputeID, uint _ruling) internal;&#13;
}&#13;
&#13;
/**&#13;
 *  @title Arbitrable Permission List&#13;
 *  @dev This is an arbitrator curated registry. Anyone can post an item with a deposit. If no one complains within a defined time period, the item is added to the registry.&#13;
 *  Anyone can complain and also post a deposit. If someone does, a dispute is created. The winner of the dispute gets the deposit of the other party and the item is added or removed accordingly.&#13;
 *  To make a request, parties have to deposit a stake and the arbitration fees. If the arbitration fees change between the submitter's payment and the challenger's payment, a part of the submitter stake can be used as an arbitration fee deposit.&#13;
 *  In case the arbitrator refuses to rule, the item is put in the initial absent status and the balance is split equally between parties.&#13;
 */&#13;
contract ArbitrablePermissionList is PermissionInterface, Arbitrable {&#13;
    /* Enums */&#13;
&#13;
    enum ItemStatus {&#13;
        Absent, // The item has never been submitted.&#13;
        Cleared, // The item has been submitted and the dispute resolution process determined it should not be added or a clearing request has been submitted and not contested.&#13;
        Resubmitted, // The item has been cleared but someone has resubmitted it.&#13;
        Registered, // The item has been submitted and the dispute resolution process determined it should be added or the submission was never contested.&#13;
        Submitted, // The item has been submitted.&#13;
        ClearingRequested, // The item is registered, but someone has requested to remove it.&#13;
        PreventiveClearingRequested // The item has never been registered, but someone asked to clear it preemptively to avoid it being shown as not registered during the dispute resolution process.&#13;
    }&#13;
&#13;
    /* Structs */&#13;
&#13;
    struct Item {&#13;
        ItemStatus status; // Status of the item.&#13;
        uint lastAction; // Time of the last action.&#13;
        address submitter; // Address of the submitter of the item status change request, if any.&#13;
        address challenger; // Address of the challenger, if any.&#13;
        uint balance; // The total amount of funds to be given to the winner of a potential dispute. Includes stake and reimbursement of arbitration fees.&#13;
        bool disputed; // True if a dispute is taking place.&#13;
        uint disputeID; // ID of the dispute, if any.&#13;
    }&#13;
&#13;
    /* Events */&#13;
&#13;
    /**&#13;
     *  @dev Called when the item's status changes or when it is contested/resolved.&#13;
     *  @param submitter Address of the submitter, if any.&#13;
     *  @param challenger Address of the challenger, if any.&#13;
     *  @param value The value of the item.&#13;
     *  @param status The status of the item.&#13;
     *  @param disputed The item is being disputed.&#13;
     */&#13;
    event ItemStatusChange(&#13;
        address indexed submitter,&#13;
        address indexed challenger,&#13;
        bytes32 indexed value,&#13;
        ItemStatus status,&#13;
        bool disputed&#13;
    );&#13;
&#13;
    /* Storage */&#13;
&#13;
    // Settings&#13;
    bool public blacklist; // True if the list should function as a blacklist, false if it should function as a whitelist.&#13;
    bool public appendOnly; // True if the list should be append only.&#13;
    bool public rechallengePossible; // True if items winning their disputes can be challenged again.&#13;
    uint public stake; // The stake to put to submit/clear/challenge and item in addition of arbitration fees.&#13;
    uint public timeToChallenge; // The time before which an action is executable if not challenged.&#13;
&#13;
    // Ruling Options&#13;
    uint8 constant REGISTER = 1;&#13;
    uint8 constant CLEAR = 2;&#13;
&#13;
    // Items&#13;
    mapping(bytes32 =&gt; Item) public items;&#13;
    mapping(uint =&gt; bytes32) public disputeIDToItem;&#13;
    bytes32[] public itemsList;&#13;
&#13;
    /* Constructor */&#13;
&#13;
    /**&#13;
     *  @dev Constructs the arbitrable permission list and sets the type.&#13;
     *  @param _arbitrator The chosen arbitrator.&#13;
     *  @param _arbitratorExtraData Extra data for the arbitrator contract.&#13;
     *  @param _metaEvidence The URL of the meta evidence object.&#13;
     *  @param _blacklist True if the list should function as a blacklist, false if it should function as a whitelist.&#13;
     *  @param _appendOnly True if the list should be append only.&#13;
     *  @param _rechallengePossible True if it is possible to challenge again a submission which has won a dispute.&#13;
     *  @param _stake The amount in Weis of deposit required for a submission or a challenge in addition of the arbitration fees.&#13;
     *  @param _timeToChallenge The time in seconds, other parties have to challenge.&#13;
     */&#13;
    constructor(&#13;
        Arbitrator _arbitrator,&#13;
        bytes _arbitratorExtraData,&#13;
        string _metaEvidence,&#13;
        bool _blacklist,&#13;
        bool _appendOnly,&#13;
        bool _rechallengePossible,&#13;
        uint _stake,&#13;
        uint _timeToChallenge) Arbitrable(_arbitrator, _arbitratorExtraData) public {&#13;
        emit MetaEvidence(0, _metaEvidence);&#13;
        blacklist = _blacklist;&#13;
        appendOnly = _appendOnly;&#13;
        rechallengePossible = _rechallengePossible;&#13;
        stake = _stake;&#13;
        timeToChallenge = _timeToChallenge;&#13;
    }&#13;
&#13;
    /* Public */&#13;
&#13;
    /**&#13;
     *  @dev Request for an item to be registered.&#13;
     *  @param _value The value of the item to register.&#13;
     */&#13;
    function requestRegistration(bytes32 _value) public payable {&#13;
        Item storage item = items[_value];&#13;
        uint arbitratorCost = arbitrator.arbitrationCost(arbitratorExtraData);&#13;
        require(msg.value &gt;= stake + arbitratorCost);&#13;
&#13;
        if (item.status == ItemStatus.Absent)&#13;
            item.status = ItemStatus.Submitted;&#13;
        else if (item.status == ItemStatus.Cleared)&#13;
            item.status = ItemStatus.Resubmitted;&#13;
        else&#13;
            revert(); // If the item is neither Absent nor Cleared, it is not possible to request registering it.&#13;
&#13;
        if (item.lastAction == 0) {&#13;
            itemsList.push(_value);&#13;
        }&#13;
&#13;
        item.submitter = msg.sender;&#13;
        item.balance += msg.value;&#13;
        item.lastAction = now;&#13;
&#13;
        emit ItemStatusChange(item.submitter, item.challenger, _value, item.status, item.disputed);&#13;
    }&#13;
&#13;
    /**&#13;
     *  @dev Request an item to be cleared.&#13;
     *  @param _value The value of the item to clear.&#13;
     */&#13;
    function requestClearing(bytes32 _value) public payable {&#13;
        Item storage item = items[_value];&#13;
        uint arbitratorCost = arbitrator.arbitrationCost(arbitratorExtraData);&#13;
        require(!appendOnly);&#13;
        require(msg.value &gt;= stake + arbitratorCost);&#13;
&#13;
        if (item.status == ItemStatus.Registered)&#13;
            item.status = ItemStatus.ClearingRequested;&#13;
        else if (item.status == ItemStatus.Absent)&#13;
            item.status = ItemStatus.PreventiveClearingRequested;&#13;
        else&#13;
            revert(); // If the item is neither Registered nor Absent, it is not possible to request clearing it.&#13;
        &#13;
        if (item.lastAction == 0) {&#13;
            itemsList.push(_value);&#13;
        }&#13;
&#13;
        item.submitter = msg.sender;&#13;
        item.balance += msg.value;&#13;
        item.lastAction = now;&#13;
&#13;
        emit ItemStatusChange(item.submitter, item.challenger, _value, item.status, item.disputed);&#13;
    }&#13;
&#13;
    /**&#13;
     *  @dev Challenge a registration request.&#13;
     *  @param _value The value of the item subject to the registering request.&#13;
     */&#13;
    function challengeRegistration(bytes32 _value) public payable {&#13;
        Item storage item = items[_value];&#13;
        uint arbitratorCost = arbitrator.arbitrationCost(arbitratorExtraData);&#13;
        require(msg.value &gt;= stake + arbitratorCost);&#13;
        require(item.status == ItemStatus.Resubmitted || item.status == ItemStatus.Submitted);&#13;
        require(!item.disputed);&#13;
&#13;
        if (item.balance &gt;= arbitratorCost) { // In the general case, create a dispute.&#13;
            item.challenger = msg.sender;&#13;
            item.balance += msg.value-arbitratorCost;&#13;
            item.disputed = true;&#13;
            item.disputeID = arbitrator.createDispute.value(arbitratorCost)(2,arbitratorExtraData);&#13;
            disputeIDToItem[item.disputeID] = _value;&#13;
            emit Dispute(arbitrator, item.disputeID, 0);&#13;
        } else { // In the case the arbitration fees increased so much that the deposit of the requester is not high enough. Cancel the request.&#13;
            if (item.status == ItemStatus.Resubmitted)&#13;
                item.status = ItemStatus.Cleared;&#13;
            else&#13;
                item.status = ItemStatus.Absent;&#13;
&#13;
            item.submitter.send(item.balance); // Deliberate use of send in order to not block the contract in case of reverting fallback.&#13;
            item.balance = 0;&#13;
            msg.sender.transfer(msg.value);&#13;
        }&#13;
&#13;
        item.lastAction = now;&#13;
&#13;
        emit ItemStatusChange(item.submitter, item.challenger, _value, item.status, item.disputed);&#13;
    }&#13;
&#13;
    /**&#13;
     *  @dev Challenge a clearing request.&#13;
     *  @param _value The value of the item subject to the clearing request.&#13;
     */&#13;
    function challengeClearing(bytes32 _value) public payable {&#13;
        Item storage item = items[_value];&#13;
        uint arbitratorCost = arbitrator.arbitrationCost(arbitratorExtraData);&#13;
        require(msg.value &gt;= stake + arbitratorCost);&#13;
        require(item.status == ItemStatus.ClearingRequested || item.status == ItemStatus.PreventiveClearingRequested);&#13;
        require(!item.disputed);&#13;
&#13;
        if (item.balance &gt;= arbitratorCost) { // In the general case, create a dispute.&#13;
            item.challenger = msg.sender;&#13;
            item.balance += msg.value-arbitratorCost;&#13;
            item.disputed = true;&#13;
            item.disputeID = arbitrator.createDispute.value(arbitratorCost)(2,arbitratorExtraData);&#13;
            disputeIDToItem[item.disputeID] = _value;&#13;
            emit Dispute(arbitrator, item.disputeID, 0);&#13;
        } else { // In the case the arbitration fees increased so much that the deposit of the requester is not high enough. Cancel the request.&#13;
            if (item.status == ItemStatus.ClearingRequested)&#13;
                item.status = ItemStatus.Registered;&#13;
            else&#13;
                item.status = ItemStatus.Absent;&#13;
&#13;
            item.submitter.send(item.balance); // Deliberate use of send in order to not block the contract in case of reverting fallback.&#13;
            item.balance = 0;&#13;
            msg.sender.transfer(msg.value);&#13;
        }&#13;
&#13;
        item.lastAction = now;&#13;
&#13;
        emit ItemStatusChange(item.submitter, item.challenger, _value, item.status, item.disputed);&#13;
    }&#13;
&#13;
    /**&#13;
     *  @dev Appeal ruling. Anyone can appeal to prevent a malicious actor from challenging its own submission and loosing on purpose.&#13;
     *  @param _value The value of the item with the dispute to appeal on.&#13;
     */&#13;
    function appeal(bytes32 _value) public payable {&#13;
        Item storage item = items[_value];&#13;
        arbitrator.appeal.value(msg.value)(item.disputeID,arbitratorExtraData); // Appeal, no need to check anything as the arbitrator does it.&#13;
    }&#13;
&#13;
    /**&#13;
     *  @dev Execute a request after the time for challenging it has passed. Can be called by anyone.&#13;
     *  @param _value The value of the item with the request to execute.&#13;
     */&#13;
    function executeRequest(bytes32 _value) public {&#13;
        Item storage item = items[_value];&#13;
        require(now - item.lastAction &gt;= timeToChallenge);&#13;
        require(!item.disputed);&#13;
&#13;
        if (item.status == ItemStatus.Resubmitted || item.status == ItemStatus.Submitted)&#13;
            item.status = ItemStatus.Registered;&#13;
        else if (item.status == ItemStatus.ClearingRequested || item.status == ItemStatus.PreventiveClearingRequested)&#13;
            item.status = ItemStatus.Cleared;&#13;
        else&#13;
            revert();&#13;
&#13;
        item.submitter.send(item.balance); // Deliberate use of send in order to not block the contract in case of reverting fallback.&#13;
&#13;
        emit ItemStatusChange(item.submitter, item.challenger, _value, item.status, item.disputed);&#13;
    }&#13;
&#13;
    /* Public Views */&#13;
&#13;
    /**&#13;
     *  @dev Return true if the item is allowed. &#13;
     *  We consider the item to be in the list if its status is contested and it has not won a dispute previously.&#13;
     *  @param _value The value of the item to check.&#13;
     *  @return allowed True if the item is allowed, false otherwise.&#13;
     */&#13;
    function isPermitted(bytes32 _value) public view returns (bool allowed) {&#13;
        Item storage item = items[_value];&#13;
        bool _excluded = item.status &lt;= ItemStatus.Resubmitted ||&#13;
            (item.status == ItemStatus.PreventiveClearingRequested &amp;&amp; !item.disputed);&#13;
        return blacklist ? _excluded : !_excluded; // Items excluded from blacklist should return true.&#13;
    }&#13;
&#13;
    /* Internal */&#13;
&#13;
    /**&#13;
     *  @dev Execute the ruling of a dispute.&#13;
     *  @param _disputeID ID of the dispute in the Arbitrator contract.&#13;
     *  @param _ruling Ruling given by the arbitrator. Note that 0 is reserved for "Not able/wanting to make a decision".&#13;
     */&#13;
    function executeRuling(uint _disputeID, uint _ruling) internal {&#13;
        Item storage item = items[disputeIDToItem[_disputeID]];&#13;
        require(item.disputed);&#13;
&#13;
        if (_ruling == REGISTER) {&#13;
            if (rechallengePossible &amp;&amp; item.status==ItemStatus.Submitted) {&#13;
                uint arbitratorCost = arbitrator.arbitrationCost(arbitratorExtraData);&#13;
                if (arbitratorCost + stake &lt; item.balance) { // Check that the balance is enough.&#13;
                    uint toSend = item.balance - (arbitratorCost + stake);&#13;
                    item.submitter.send(toSend); // Keep the arbitration cost and the stake and send the remaining to the submitter.&#13;
                    item.balance -= toSend;&#13;
                }&#13;
            } else {&#13;
                if (item.status==ItemStatus.Resubmitted || item.status==ItemStatus.Submitted)&#13;
                    item.submitter.send(item.balance); // Deliberate use of send in order to not block the contract in case of reverting fallback.&#13;
                else&#13;
                    item.challenger.send(item.balance);&#13;
                    &#13;
                item.status = ItemStatus.Registered;&#13;
            }&#13;
        } else if (_ruling == CLEAR) {&#13;
            if (item.status == ItemStatus.PreventiveClearingRequested || item.status == ItemStatus.ClearingRequested)&#13;
                item.submitter.send(item.balance);&#13;
            else&#13;
                item.challenger.send(item.balance);&#13;
&#13;
            item.status = ItemStatus.Cleared;&#13;
        } else { // Split the balance 50-50 and give the item the initial status.&#13;
            if (item.status==ItemStatus.Resubmitted)&#13;
                item.status = ItemStatus.Cleared;&#13;
            else if (item.status==ItemStatus.ClearingRequested)&#13;
                item.status = ItemStatus.Registered;&#13;
            else&#13;
                item.status = ItemStatus.Absent;&#13;
            item.submitter.send(item.balance / 2);&#13;
            item.challenger.send(item.balance / 2);&#13;
        }&#13;
        &#13;
        item.disputed = false;&#13;
        if (rechallengePossible &amp;&amp; item.status==ItemStatus.Submitted &amp;&amp; _ruling==REGISTER) &#13;
            item.lastAction=now; // If the item can be rechallenged, update the time and keep the remaining balance.&#13;
        else&#13;
            item.balance = 0;&#13;
&#13;
        emit ItemStatusChange(item.submitter, item.challenger, disputeIDToItem[_disputeID], item.status, item.disputed);&#13;
    }&#13;
&#13;
    /* Interface Views */&#13;
&#13;
    /**&#13;
     *  @dev Return the number of items in the list.&#13;
     *  @return The number of items in the list.&#13;
     */&#13;
    function itemsCount() public view returns (uint count) {&#13;
        count = itemsList.length;&#13;
    }&#13;
&#13;
    /**&#13;
     *  @dev Return the numbers of items in the list per status.&#13;
     *  @return The numbers of items in the list per status.&#13;
     */&#13;
    function itemsCounts() public view returns (uint pending, uint challenged, uint accepted, uint rejected) {&#13;
        for (uint i = 0; i &lt; itemsList.length; i++) {&#13;
            Item storage item = items[itemsList[i]];&#13;
            if (item.disputed) challenged++;&#13;
            else if (item.status == ItemStatus.Resubmitted || item.status == ItemStatus.Submitted) pending++;&#13;
            else if (item.status == ItemStatus.Registered) accepted++;&#13;
            else if (item.status == ItemStatus.Cleared) rejected++;&#13;
        }&#13;
    }&#13;
&#13;
    /**&#13;
     *  @dev Return the values of the items the query finds.&#13;
     *  This function is O(n) at worst, where n is the number of items. This could exceed the gas limit, therefore this function should only be used for interface display and not by other contracts.&#13;
     *  @param _cursor The pagination cursor.&#13;
     *  @param _count The number of items to return.&#13;
     *  @param _filter The filter to use.&#13;
     *  @param _sort The sort order to use.&#13;
     *  @return The values of the items found and wether there are more items for the current filter and sort.&#13;
     */&#13;
    function queryItems(bytes32 _cursor, uint _count, bool[6] _filter, bool _sort) public view returns (bytes32[] values, bool hasMore) {&#13;
        uint _cursorIndex;&#13;
        values = new bytes32[](_count);&#13;
        uint _index = 0;&#13;
&#13;
        if (_cursor == 0)&#13;
            _cursorIndex = 0;&#13;
        else {&#13;
            for (uint j = 0; j &lt; itemsList.length; j++) {&#13;
                if (itemsList[j] == _cursor) {&#13;
                    _cursorIndex = j;&#13;
                    break;&#13;
                }&#13;
            }&#13;
            require(_cursorIndex != 0);&#13;
        }&#13;
&#13;
        for (&#13;
                uint i = _cursorIndex == 0 ? (_sort ? 0 : 1) : (_sort ? _cursorIndex + 1 : itemsList.length - _cursorIndex + 1);&#13;
                _sort ? i &lt; itemsList.length : i &lt;= itemsList.length;&#13;
                i++&#13;
            ) { // Oldest or newest first&#13;
            Item storage item = items[itemsList[_sort ? i : itemsList.length - i]];&#13;
            if (&#13;
                item.status != ItemStatus.Absent &amp;&amp; item.status != ItemStatus.PreventiveClearingRequested &amp;&amp; (&#13;
                    (_filter[0] &amp;&amp; (item.status == ItemStatus.Resubmitted || item.status == ItemStatus.Submitted)) || // Pending&#13;
                    (_filter[1] &amp;&amp; item.disputed) || // Challenged&#13;
                    (_filter[2] &amp;&amp; item.status == ItemStatus.Registered) || // Accepted&#13;
                    (_filter[3] &amp;&amp; item.status == ItemStatus.Cleared) || // Rejected&#13;
                    (_filter[4] &amp;&amp; item.submitter == msg.sender) || // My Submissions&#13;
                    (_filter[5] &amp;&amp; item.challenger == msg.sender) // My Challenges&#13;
                )&#13;
            ) {&#13;
                if (_index &lt; _count) {&#13;
                    values[_index] = itemsList[_sort ? i : itemsList.length - i];&#13;
                    _index++;&#13;
                } else {&#13;
                    hasMore = true;&#13;
                    break;&#13;
                }&#13;
            }&#13;
        }&#13;
    }&#13;
}